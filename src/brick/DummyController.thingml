import "Controller.thingml"

thing DummyController includes Controller, TimerMsgs {
	
	readonly property timerID : UInt8 = 3 // Make sure to use a timer which is not already used!
	
	required port clock {
		sends timer_start, timer_cancel
		receives timer_timeout
	}
	
	readonly property XMAX : Int16 = 100
	readonly property YMAX : Int16 = 100
	readonly property XMIN : Int16 = -100
	readonly property YMIN : Int16 = -100
	
	property posX : Int16 = 0
	property posY : Int16 = 0
	
	property dx : Int16 = 5
	property dy : Int16 = 5
	
	statechart SC init Bouncing {
		
		state Bouncing {
			on entry clock!timer_start(timerID, 50)
			
			transition -> Bouncing event e : clock?timer_timeout guard e.id == timerID
			action do
				posX = posX + dx
				posY = posY + dy
				
				if (posX < XMIN) 		do 	posX = 2*XMIN-posX		dx = -dx	end
				else if (posX > XMAX) 	do 	posX = 2*XMAX-posX		dx = -dx	end
				
				if (posY < YMIN) 		do 	posY = 2*YMIN-posY		dy = -dy	end
				else if (posY > YMAX) 	do 	posY = 2*YMAX-posY		dy = -dy	end
				
				controls!position(posX, posY)
			end	
		}
	}
}
